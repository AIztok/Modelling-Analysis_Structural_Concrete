!#!Info  Example:       Input file for RELY-Benchmark Example 3
!#!Info  Keyword:       Reliability Anaylsis; FEM
!#!Info  Program:       RELY

! ==========================================================
! Finite element model of Benchmark example - 3
! ==========================================================
!
! ----------------------------------------------------------
! Description of the example
! ----------------------------------------------------------
!
! -> see Benchmark example 3.
!
! ----------------------------------------------------------
! Deterministic parameter configuration
! ----------------------------------------------------------
!
! NOTE: the following template must be deaktivated if a
!       stochastic analysis is performed.
!       The template needs to be activated if a
!       deterministic run of the FE model should be
!       performed.
!
-prog template urs:1
HEAD DEACTIVATE FOR STOCHASTIC ANALYSIS
$  sto#rv_q      8.          $ [kN/m]
  sto#rv_g      1.          $ [-] Factor of Deadload
  sto#rv_sFy    560.        $ [N/mm^2]
  sto#rv_cFc    45.         $ [N/mm^2]
END
!
! ----------------------------------------------------------
! Definition of the cross-section of the beam
! ----------------------------------------------------------
!
! Note: the compressive strength of concrete (#rv_cFc) as
!       well as the yield strength of steel is uncertain.
!
+PROG AQUA urs:2
HEAD Plane Beam
$----------------------------------------------------------------------------------
!*! Specification of the standard
NORM OEN en199X-200X unit 0 !

$----------------------------------------------------------------------------------
!*! Concrete
CONC NO 1  TYPE c 30N           titl 'Conc. C30/37'

$----------------------------------------------------------------------------------
!*! Reinforcement
STEE NO 101 TYPE b 550b FY #rv_sFy  FT #rv_sFy titl 'Reinf. B550B'

$----------------------------------------------------------------------------------
!*! T-beam
SREC 1 H 0.8 B 0.3 HO 0.2 BO 2.5 MNO 1 MRF 101 SO 0.04[m] titl 'T-beam simple'


END
!
! ----------------------------------------------------------
! Definition of the mesh
! ----------------------------------------------------------
+prog sofimshc urs:2
head 'Model'
$----------------------------------------------------------------------------------
!*! System and mesh
ctrl mesh 1 $ 1 - Stabmodell, 2 - Flächenmodell, 3 - Volumenmodell
ctrl hmin 0.25 $[m] Größe Finite Elemente

syst 3D gdir posz gdiv 100

$----------------------------------------------------------------------------------
!*! Structural points
spt no  x       y       z       fix
    21  0       0       0       ppmx   $ support in X,Y,Z and Rotation around X-Axis
    22  12      0       0       xpmx   $ support in all direction except X-Axis and Rotation around X-Axis

$----------------------------------------------------------------------------------
!*! Structural lines
$ Beam
sln 20 21 22 sno 1 grp 1 styp 'b.e'

end
!
! ----------------------------------------------------------
! Definition of loading
! ----------------------------------------------------------
!
! Note: the value of the loading (#rv_q) is uncertain.
!
+PROG SOFILOAD urs:4
HEAD Dead load
	LC 1 facd #rv_g
$	BEAM GRP 1 PA #rv_g
END
!
+PROG ASE urs:5
HEAD Structural response

syst prob line

LC 1

END
!
! ----------------------------------------------------------
! Compute the required reinforcement
! ----------------------------------------------------------
!
! Note: the current value of the limit-state function is
!       stored in variable 'rel_fun'.
!
+PROG AQB urs:6
HEAD Reinforcement design
ECHO BSEC  FULL
LC 1 ; BEAM GRP 1
COMB EXTR  SOLO LC1 1 F1 1.00 $ gemischter Lastfaktor

DESI ULTI SC1 1.0 SS1 1.0 $ material safety factor set to 1.0

END
!
! ----------------------------------------------------------
! Explicitly store the current value of the limit-state
! ----------------------------------------------------------
!
! Note: the current value of the limit-state function is
!       stored in variable 'rel_fun'.
!
+PROG TEMPLATE urs:7
HEAD  Performance function

  $ provided amount of reinforcement
  LET#RF_PROV 10.7 $ [cm2]

  $ get max reinforcement from rf-design
  LET#DC 1
  LET#CDB_IER 0
  LET#RF_REQ 0.0
  $ read all sections of beam number 10025 (mid span)
  @KEY BEAM_RFC #DC 125
  LOOP
     LET#DUMMY @(ASUM) $ get computed total longitudinal reinforcement
     IF #CDB_IER>=0
        LET#RF_REQ MAX(#RF_REQ,#DUMMY)
     ENDIF
  ENDLOOP #CDB_IER<2

  LET#RF_REQ #RF_REQ*10000  ; PRT#RF_REQ   $ max reinforcement in [cm2]

  $ performance function
  STO#rel_fun (#RF_PROV-#RF_REQ)
  PRT#rel_fun

END

